/*
    //------------------------------------------------------------------------------------
    //      Мультиплексор потокового интерфейса DataStream
    //      (полностью комбинационный)
    ds_mux
    #(
        .WIDTH      (), // Разрядность потока
        .INPUTS     ()  // Количество входных интерфейсов (более 1-го)
    )
    the_ds_mux
    (
        // Сброс и тактирование
        .reset      (), // i  Не используется
        .clk        (), // i  Не используется
        
        // Шина выбора активного входа
        .select     (), // i  [$clog2(INPUTS) - 1 : 0]
        
        // Входные потоковые интерфейсы
        .i_dat      (), // i  [INPUTS - 1 : 0][WIDTH - 1 : 0]
        .i_val      (), // i  [INPUTS - 1 : 0]
        .i_rdy      (), // o  [INPUTS - 1 : 0]
        
        // Выходной потоковый интерфейс
        .o_dat      (), // o  [WIDTH - 1 : 0]
        .o_val      (), // o
        .o_rdy      ()  // i
    ); // the_ds_mux
*/

module ds_mux
#(
    parameter int unsigned                          WIDTH   = 8,    // Разрядность потока
    parameter int unsigned                          INPUTS  = 2     // Количество входных интерфейсов (более 1-го)
)
(
    // Сброс и тактирование
    input  logic                                    reset,          // Не используется
    input  logic                                    clk,            // Не используется
    
    // Шина выбора активного входа
    input  logic [$clog2(INPUTS) - 1 : 0]           select,
    
    // Входные потоковые интерфейсы
    input  logic [INPUTS - 1 : 0][WIDTH - 1 : 0]    i_dat,
    input  logic [INPUTS - 1 : 0]                   i_val,
    output logic [INPUTS - 1 : 0]                   i_rdy,
    
    // Выходной потоковый интерфейс
    output logic [WIDTH - 1 : 0]                    o_dat,
    output logic                                    o_val,
    input  logic                                    o_rdy
);
    //------------------------------------------------------------------------------------
    //      Описание сигналов
    logic [INPUTS - 1 : 0]                          select_pos;
    
    //------------------------------------------------------------------------------------
    //      Позиционный код выбираемого канала
    always_comb begin
        select_pos = {INPUTS{1'b0}};
        select_pos[select] = 1'b1;
    end
    
    //------------------------------------------------------------------------------------
    //      Формирование сигналов готовности приемника входных интерфейсов
    assign i_rdy = select_pos & {INPUTS{o_rdy}};
    
    //------------------------------------------------------------------------------------
    //      Формирование сигналов выходного потокового интерфейса
    assign o_dat = i_dat[select];
    assign o_val = i_val[select];
    
endmodule // ds_mux